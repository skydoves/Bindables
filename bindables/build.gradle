///*
// * Designed and developed by 2021 skydoves (Jaewoong Eum)
// *
// * Licensed under the Apache License, Version 2.0 (the "License");
// * you may not use this file except in compliance with the License.
// * You may obtain a copy of the License at
// *
// * http://www.apache.org/licenses/LICENSE-2.0
// *
// * Unless required by applicable law or agreed to in writing, software
// * distributed under the License is distributed on an "AS IS" BASIS,
// * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// * See the License for the specific language governing permissions and
// * limitations under the License.
// */
plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'org.jetbrains.dokka'
    id 'binary-compatibility-validator'
}

apply from: "$rootDir/dependencies.gradle"

android {
    compileSdkVersion versions.compileSdk
    defaultConfig {
        minSdkVersion versions.minSdk
        targetSdkVersion versions.compileSdk
        versionCode versions.versionCode
        versionName versions.versionName
        consumerProguardFiles "consumer-rules.pro"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        buildConfig false
        dataBinding true
    }
}

apiValidation {
    ignoredPackages += [
        "com/skydoves/bindables/databinding",
    ]
    nonPublicMarkers += [
        "kotlin.PublishedApi",
    ]
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions.freeCompilerArgs += ["-Xexplicit-api=strict"]
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-reflect:$versions.kotlin"
    implementation "com.google.android.material:material:$versions.googleMaterial"
    implementation "androidx.appcompat:appcompat:$versions.androidxAppcompat"
    api "androidx.lifecycle:lifecycle-viewmodel-ktx:$versions.lifecycleVersion"
    api "androidx.lifecycle:lifecycle-viewmodel-savedstate:$versions.lifecycleVersion"
    api "androidx.recyclerview:recyclerview:$versions.recyclerView"
}

apply plugin: "com.vanniktech.maven.publish"
apply from: "$rootDir/spotless.gradle"